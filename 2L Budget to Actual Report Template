ENGINE INT CACHE SET ON
SET COMPOUND=BYTOC
-SET &POSTING_FP='06' ;
-*-SET &PROJECTION_FP=&POSTING_FP+1 ;
-SET &FISCAL_PERIOD_DECODE=DECODE '&POSTING_FP'(9999 All 06 January 07 February 08 March 09 April 10 May 11 June 12 July 01 August 02 September 03 October 04 November 05 December) ;


-*COMPONENT=Define_GL_CURRENT
DEFINE FILE GL_CURRENT
 PREV_ACTUAL/D20=IF GL_CURRENT.DATES.POSTING_FISCAL_YEAR EQ '&PREV_FY' AND GL_CURRENT.OBJECT.OBJECT IN ( '3306' , '3307' , '3308' , '3309' ) THEN GL_CURRENT.BALANCES.ACTUAL *  -1 ELSE IF GL_CURRENT.DATES.POSTING_FISCAL_YEAR EQ '&PREV_FY' THEN GL_CURRENT.BALANCES.ACTUAL ELSE 0 ;
 CURR_BUD/D20=0 ;
 ACT_ENCUM/D20=0 ;
END
-DEFAULTH &WF_SUMMARY='Summary';
-DEFAULTH &WF_TITLE='WebFOCUS Report';
TABLE FILE GL_CURRENT
SUM PREV_ACTUAL
CURR_BUD
ACT_ENCUM
BY GL_CURRENT.DATES.POSTING_FISCAL_YEAR
BY GL_CURRENT.DATES.POSTING_FISCAL_PERIOD
BY GL_CURRENT.ORGANIZATION.ORGANIZATION
BY GL_CURRENT.ORGANIZATION.ORGANIZATION
BY GL_CURRENT.ORGANIZATION.ORGANIZATION_NAME
BY GL_CURRENT.ACCOUNT.ACCOUNT
BY GL_CURRENT.ACCOUNT.ACCOUNT_NAME
BY GL_CURRENT.CONSOLIDATION.CONSOLIDATION_OBJECT_DESC
BY GL_CURRENT.OBJ_TYPE.OBJECT_TYPE_DESC
BY GL_CURRENT.OBJECT.OBJECT
BY GL_CURRENT.LEVEL.OBJECT_LEVEL
BY GL_CURRENT.LEVEL.OBJECT_LEVEL_DESC
BY GL_CURRENT.OBJECT.OBJECT
BY GL_CURRENT.OBJECT.OBJECT_DESC
BY GL_CURRENT.TRANSACTION_DETAILS.TRANSACTION_EDOC
BY GL_CURRENT.TRANSACTION_DETAILS.TRANSACTION_DESCRIPTION
WHERE GL_CURRENT.ORGANIZATION.ORGANIZATION EQ '1858';
WHERE GL_CURRENT.DATES.POSTING_FISCAL_YEAR EQ &PREV_FY. (|FORMAT=A40V).QUOTEDSTRING;
WHERE GL_CURRENT.CONSOLIDATION.CONSOLIDATION_OBJECT_DESC NE 'Assets-Current' OR 'Assets-Noncurrent' OR 'Capital Projects' OR 'Encumbrances' OR 'Liabilities-Current' OR 'Liabilities-Noncurrent';
WHERE GL_CURRENT.ACCOUNT.ACCOUNT EQ '2$*';
WHERE GL_CURRENT.FUND_GROUP.FUND_GROUP EQ 'CU';
ON TABLE HOLD AS H_PREVFY FORMAT XFOCUS
ON TABLE NOTOTAL
ON TABLE SET CACHELINES 100
ON TABLE SET PAGE-NUM NOLEAD
ON TABLE SET SQUEEZE ON
ON TABLE SET HTMLCSS ON
ON TABLE SET HTMLENCODE ON
ON TABLE SET BYDISPLAY ON
ON TABLE SET EMPTYREPORT ON
ON TABLE SET GRWIDTH 1
ON TABLE SET STYLE *
INCLUDE=IBFS:/FILE/IBI_HTML_DIR/javaassist/intl/EN/ENIADefault_combine.sty,$
TYPE=REPORT, TITLETEXT=&WF_TITLE.QUOTEDSTRING, SUMMARY=&WF_SUMMARY.QUOTEDSTRING, HFREEZE=OFF, $
ENDSTYLE
END

-RUN

-*COMPONENT=Define_GL_CURRENT
DEFINE FILE GL_CURRENT
 PREV_ACTUAL/D20=0 ;
 CURR_BUD/D20=IF GL_CURRENT.DATES.POSTING_FISCAL_YEAR EQ '&CURR_FY' AND GL_CURRENT.OBJECT.OBJECT IN ( '3306','3307', '3308','3309' ) THEN GL_CURRENT.BALANCES.CURRENT_BUDGET *  -1 ELSE IF GL_CURRENT.DATES.POSTING_FISCAL_YEAR EQ '&CURR_FY' THEN GL_CURRENT.BALANCES.CURRENT_BUDGET ELSE 0 ;
 ACT_ENCUM/D20=IF GL_CURRENT.DATES.POSTING_FISCAL_YEAR EQ '&CURR_FY' AND GL_CURRENT.OBJECT.OBJECT IN ( '3306','3307', '3308','3309' ) THEN ( GL_CURRENT.BALANCES.ACTUAL + GL_CURRENT.BALANCES.ENCUMBRANCE ) *  -1 ELSE IF GL_CURRENT.DATES.POSTING_FISCAL_YEAR EQ '&CURR_FY' THEN ( GL_CURRENT.BALANCES.ACTUAL + GL_CURRENT.BALANCES.ENCUMBRANCE ) ELSE 0 ;
END
-DEFAULTH &WF_SUMMARY='Summary';
-DEFAULTH &WF_TITLE='WebFOCUS Report';
TABLE FILE GL_CURRENT
SUM PREV_ACTUAL
CURR_BUD
ACT_ENCUM
BY GL_CURRENT.DATES.POSTING_FISCAL_YEAR
BY GL_CURRENT.DATES.POSTING_FISCAL_PERIOD
BY GL_CURRENT.ORGANIZATION.ORGANIZATION
BY GL_CURRENT.ORGANIZATION.ORGANIZATION
BY GL_CURRENT.ORGANIZATION.ORGANIZATION_NAME
BY GL_CURRENT.ACCOUNT.ACCOUNT
BY GL_CURRENT.ACCOUNT.ACCOUNT_NAME
BY GL_CURRENT.CONSOLIDATION.CONSOLIDATION_OBJECT_DESC
BY GL_CURRENT.OBJ_TYPE.OBJECT_TYPE_DESC
BY GL_CURRENT.OBJECT.OBJECT
BY GL_CURRENT.LEVEL.OBJECT_LEVEL
BY GL_CURRENT.LEVEL.OBJECT_LEVEL_DESC
BY GL_CURRENT.OBJECT.OBJECT
BY GL_CURRENT.OBJECT.OBJECT_DESC
BY GL_CURRENT.TRANSACTION_DETAILS.TRANSACTION_EDOC
BY GL_CURRENT.TRANSACTION_DETAILS.TRANSACTION_DESCRIPTION
WHERE GL_CURRENT.ORGANIZATION.ORGANIZATION EQ '1858';
WHERE GL_CURRENT.DATES.POSTING_FISCAL_YEAR EQ &CURR_FY. (|FORMAT=A40V).QUOTEDSTRING;
-*WHERE GL_CURRENT.DATES.POSTING_FISCAL_PERIOD FROM '0' TO '&FISC_PERIOD';
WHERE GL_CURRENT.DATES.POSTING_FISCAL_PERIOD FROM '00' TO '&POSTING_FP' OR GL_CURRENT.DATES.POSTING_FISCAL_PERIOD EQ 'BB';
WHERE GL_CURRENT.CONSOLIDATION.CONSOLIDATION_OBJECT_DESC NE 'Assets-Current' OR 'Assets-Noncurrent' OR 'Capital Projects' OR 'Encumbrances' OR 'Liabilities-Current' OR 'Liabilities-Noncurrent';
WHERE GL_CURRENT.ACCOUNT.ACCOUNT EQ '2$*';
WHERE GL_CURRENT.FUND_GROUP.FUND_GROUP EQ 'CU';
ON TABLE SET BYDISPLAY ON
ON TABLE HOLD AS H_MERGE
MORE
FILE H_PREVFY
END
-RUN

DEFINE FILE H_MERGE
 OBJ_DEF/A4V=IF OBJECT_LEVEL EQ 'FTRA' AND OBJECT EQ '3206' THEN 'PTWU' 
 ELSE IF OBJECT_LEVEL EQ 'FTRA' AND OBJECT EQ '3207' THEN 'PTOU' 
 ELSE IF OBJECT_LEVEL EQ 'FTRA' AND OBJECT EQ '3208' THEN 'OTWU' 
 ELSE IF OBJECT_LEVEL EQ 'FTRA' AND OBJECT EQ '3209' THEN 'OTOU' 
 ELSE IF OBJECT_LEVEL EQ 'TRAN' AND OBJECT EQ '3306' THEN 'PTRU' 
 ELSE IF OBJECT_LEVEL EQ 'TRAN' AND OBJECT EQ '3307' THEN 'PTRO' 
 ELSE IF OBJECT_LEVEL EQ 'TRAN' AND OBJECT EQ '3308' THEN 'OTRU' 
 ELSE IF OBJECT_LEVEL EQ 'TRAN' AND OBJECT EQ '3309' THEN 'OTRO' 
 ELSE IF OBJECT_LEVEL EQ 'PERM' AND OBJECT EQ '5110' THEN 'CLAS' 
 ELSE IF OBJECT_LEVEL EQ 'PERM' AND OBJECT EQ '5111' THEN 'FACU' 
 ELSE IF OBJECT_LEVEL EQ 'PERM' AND OBJECT EQ '5112' THEN 'OTHR' 
 ELSE IF OBJECT_LEVEL EQ 'TEMP' AND OBJECT EQ '5231' THEN 'CONT' 
 ELSE IF OBJECT_LEVEL EQ 'TEMP' AND OBJECT EQ '5240' THEN 'STUD' 
 ELSE IF OBJECT_LEVEL EQ 'TEMP' AND OBJECT EQ '5250' THEN 'GRAD' 
 ELSE OBJECT_LEVEL ;
 FY_ID/A2=SUBSTV ( 4 , '&CURR_FY' , 3 , 4 , 'A2' ) ;
END
TABLE FILE H_MERGE
SUM PREV_ACTUAL
CURR_BUD
ACT_ENCUM
BY ACCOUNT_NAME NOPRINT
FOR OBJ_DEF
"Income" LABEL INCOME OVER
'BUDG' AS 'Beginning Budget Load' LABEL BUDG OVER
'FBAL' AS 'Carry Forward' LABEL FBAL OVER
" " LABEL SPACER1 OVER
"Transfer Activity" LABEL TRANSFERACT OVER
'PTWU' AS '2Ledger Perm Transfer-In (Within Unit)' LABEL PTWU OVER
'PTOU' AS '2Ledger Perm Transfer-IN (Outside Unit)' LABEL PTOU OVER
'OTWU' AS '2Ledger One-Time Trans In (Within Unit)' LABEL OTWU OVER
'OTOU' AS  '2Ledger One-Time Trans In (Outside Unit)' LABEL OTOU OVER
RECAP TOTTRANIN=PTWU+PTOU+OTWU+OTOU;
 AS 'Total Transfer In' OVER
" " LABEL SPACER2 OVER
'PTRU' AS '2Ledger Perm Transfer-Out (Within Unit)' LABEL PTRU OVER
'PTRO' AS '2Ledger Perm Transfer-Out (Outside Unit)' LABEL PTRO OVER
'OTRU' AS '2Ledger One-Time Trans Out (Within Unit)' LABEL OTRU OVER
'OTRO' AS '2Ledger One-Time Trans Out (Outside Unit)' LABEL OTRO OVER
RECAP TOTTRANOUT=PTRU+PTRO+OTRU+OTRO;
 AS 'Total Transfer Out' OVER
" " LABEL SPACER3 OVER
RECAP TOTINCOME=BUDG+FBAL+TOTTRANIN+TOTTRANOUT;
 AS 'Total Income' OVER
" " LABEL SPACER4 OVER
"Expenses" LABEL EXPENSES OVER
"Personal Services (Salaries and Fringe Benefits)" LABEL PERSONSRV OVER
'CLAS' AS 'Classified' LABEL CLASSIF OVER
'FACU' AS 'Faculty' LABEL FACULTY OVER
'OTHR' AS 'Professional Staff' LABEL PROFSTAFF OVER
'CONT' AS 'Special Payroll/Temporary/Durational' LABEL SPECPAY OVER
'STUD' AS 'Student Labor' LABEL STULABOR OVER
'GRAD' AS 'Graduate Assistants' LABEL GRADASST OVER
'OPS' AS 'Other Personal Services' LABEL OPS OVER
'FBEN' AS 'Fringe Benefits/Worker's Compensation' LABEL FRINGE OVER
RECAP TOTPERSON=CLASSIF+FACULTY+PROFSTAFF+SPECPAY+STULABOR+GRADASST+OPS+FRINGE;
 AS 'Sub Total Personal Services (Salaries & Fringe)' OVER
" " LABEL SPACER5 OVER
"General Expenses" LABEL GENEXP OVER
'DUES' AS 'DUES - Dues and Memberships' LABEL DUES OVER
'ENER' AS 'ENER - Energy/Utilities' LABEL ENER OVER
'EEPM' AS 'Employee/Student Payments' LABEL EEPM OVER
'EQCP' AS 'EQCP - Equipment - Capital' LABEL EQCP OVER
'EQNC' AS 'EQNC - Equipment - Non-Capital' LABEL EQNC OVER
'FEES' AS 'FEES - Fees' LABEL FEES OVER
'FINA' AS 'FINA - Financial Aid' LABEL FINA OVER
'OTHX' AS 'OTHX - Other Expenses' LABEL OTHX OVER
'RENT' AS 'RENT - Rentals and Leases' LABEL RENT OVER
'SERV' AS 'SERV - Services' LABEL SERV OVER
'SUBS' AS 'SUBS - Subscriptions' LABEL SUBS OVER
'SUPP' AS 'SUPP - Supplies' LABEL SUPP OVER
'TELE' AS 'TELE - Telecommunications' LABEL TELE OVER
'TRNS' AS 'TRNS - Transportation/Motor Vehicles' LABEL TRNS OVER
'TRAV' AS 'TRAV - Travel' LABEL TRAV OVER
RECAP TOTGENEXP=DUES+ENER+EEPM+EQCP+EQNC+FEES+FINA+OTHX+RENT+SERV+SUBS+SUPP+TELE+TRNS+TRAV;
 AS 'Sub Total General Expenses' OVER
" " LABEL SPACER6 OVER
RECAP TOTEXPENSES=TOTPERSON+TOTGENEXP;
 AS 'Total Expenses' OVER
" " LABEL SPACER7 OVER
RECAP NETACTIVITY=TOTINCOME-TOTEXPENSES;
 AS 'Net Fiscal Year Activity'  
-*ON TABLE SUBHEAD
HEADING
"University of Connecticut"
"<ORGANIZATION_NAME"
"<ACCOUNT_NAME"
"FY<FY_ID &FISCAL_PERIOD_DECODE Projection"
"Current Unrestricted Funds (Ledger 2)"
ON TABLE PCHOLD FORMAT XLSX
ON TABLE NOTOTAL
ON TABLE SET CACHELINES 100
ON TABLE SET PAGE-NUM NOLEAD
ON TABLE SET SQUEEZE ON
ON TABLE SET HTMLCSS ON
ON TABLE SET HTMLENCODE ON
ON TABLE SET EMPTYREPORT ON
ON TABLE SET GRWIDTH 1
ON TABLE SET STYLE *
INCLUDE=IBFS:/FILE/IBI_HTML_DIR/javaassist/intl/EN/ENIADefault_combine.sty,$
ENDSTYLE
END

-RUN

-***************************************************************************************************
-* Date Created  : 2018-01-11
-*       Author  : miw17019
-* Program Name  : 2L Budget to Actual Report Template
-* Description   : Report template for SFA Budget to Actual Reporting by Org to Acct Level 
-*-*------------------------------------------------------------------------------------------------
-* Special Attn. :
-*--------------------------------------------------------------------------------------------------
-*Change Control
-*YYYY/MM/DD   Who Chg# PROJ#   Brief Description of change
-*-----------  --- ---- ------  --------------------------------------------------------------------
-*2018/01/11   mw  0001 SFA-01  First draft of report
-***************************************************************************************************
